Import('env')
from os import path

modName = path.basename(path.abspath('..'))
libName = path.basename(path.abspath('.'))
name    = modName + "_" + libName

# --------------------------------------------------------------------------

# for this option to take affect, it must be added *before* we add the ispc sources.
env.AppendUnique(ISPCFLAGS = ['--opt=force-aligned-memory'])

components  = ['scene_rdl2', 'bvh_shading_ispc', 'shading_ispc']
env.DWAUseComponents(components)

sources, header  = env.IspcShared(['ispc/Noise.ispc',
                              'ispc/Perlin.ispc',
                              'ispc/Simplex.ispc',
                              'ispc/Worley.ispc'
                              ])
sources     += env.DWAGlob('*.cc')
public_headers = ['Noise.h',
                  'ispc/Noise_ispc_stubs.h',
                  'Worley.h',
                  'ispc/Worley_ispc_stubs.h',
                  'Perlin.h',
                  'ispc/Perlin_ispc_stubs.h',
                  'Simplex.h',
                  'ispc/Simplex_ispc_stubs.h',]
public_ispc_headers = ['ispc/Noise.isph',
                       'ispc/Worley.isph',
                       'ispc/Perlin.isph',
                       'ispc/Simplex.isph']


incdir      = [
                env.Dir('../../../..').srcnode(),
                env.Dir('../noise').abspath,
                env.Dir('../noise/ispc').abspath,
                env.Dir('.'),
                env.Dir('ispc')
              ]


# --------------------------------------------------------------------------
env.DWAInstallInclude(public_headers, 'moonshine/common/noise')
env.DWAInstallInclude(public_ispc_headers, 'moonshine/common/noise/ispc')
env.Append(CPPPATH=incdir)
lib = env.DWASharedLibrary(name, sources)
target = env.DWAInstallLib(lib)
env.DWAComponent(name, LIBS=[target], CPPPATH=incdir, COMPONENTS=components)
env.DWAInstallSConscriptStub(name, LIBS=target,
                             CPPPATH=[env.Dir('$INSTALL_DIR/include')],
                             COMPONENTS=components)
env.DWALinkValidate(name)

# TODO: re-write code to avoid performance warnings
env.AppendUnique(ISPCFLAGS = ['--wno-perf'])
